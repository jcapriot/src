import os, sys, string
sys.path.append('../../framework')
import bldutil

src = '''
genarea genhatch geninteract genmarker genmessage genpatarea genpoint
genraster1 genraster gentext genvector nulldev polyfix polysubs vecarea
'''

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    rsfroot = env.get('RSFROOT',os.environ.get('RSFROOT',sys.prefix))
    incdir = os.path.join(rsfroot,'include')        
    SConscript('../fonts/SConstruct')
    SConscript('../utilities/SConstruct')

env.Prepend(CPPPATH=['../../include'])

sources  = [x+'.c' for x in Split(src)]
includes = [x+'.h' for x in Split(src)]

objects = []
for source in Split(src):
    env.RSF_Include(source,prefix='pen_')
    obj = env.StaticObject(source+'.c')
    env.Depends(obj,source+'.h')
    env.Ignore(source+'.h',source+'.h')
    objects.append(obj)

inc = env.Command('genpen.h',includes,Action(bldutil.__merge))
lib = env.StaticLibrary('genpen',objects)
